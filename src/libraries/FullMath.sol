// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

library FullMath {
  error MulDivOverflow();

  /// @dev Full-precision floor(x * y / denominator), adapted from the standard 512-bit algorithm.
  function mulDiv(uint256 x, uint256 y, uint256 denominator)
    internal
    pure
    returns (uint256 result)
  {
    unchecked {
      uint256 prod0;
      uint256 prod1;
      assembly ("memory-safe") {
        let mm := mulmod(x, y, not(0))
        prod0 := mul(x, y)
        prod1 := sub(sub(mm, prod0), lt(mm, prod0))
      }
      if (prod1 == 0) return prod0 / denominator;
      if (denominator <= prod1) revert MulDivOverflow();

      uint256 remainder;
      assembly ("memory-safe") {
        remainder := mulmod(x, y, denominator)
        prod1 := sub(prod1, gt(remainder, prod0))
        prod0 := sub(prod0, remainder)
      }
      uint256 twos = denominator & (~denominator + 1);
      assembly ("memory-safe") {
        denominator := div(denominator, twos)
        prod0 := div(prod0, twos)
        twos := add(div(sub(0, twos), twos), 1)
      }
      prod0 |= prod1 * twos;
      uint256 inverse = (3 * denominator) ^ 2;
      inverse *= 2 - denominator * inverse;
      inverse *= 2 - denominator * inverse;
      inverse *= 2 - denominator * inverse;
      inverse *= 2 - denominator * inverse;
      inverse *= 2 - denominator * inverse;
      inverse *= 2 - denominator * inverse;
      result = prod0 * inverse;
    }
  }

  function mulDivUp(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256) {
    uint256 result = mulDiv(x, y, denominator);
    if (mulmod(x, y, denominator) != 0) {
      if (result == type(uint256).max) revert MulDivOverflow();
      ++result;
    }
    return result;
  }
}
